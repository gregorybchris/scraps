volume #cylinder {h = 4, r = 5}

-- Solid :: #cylinder { h : Float, r : Float }
--        | #cube Float
--        | #sphere Float
--        | #cone { h : Float, r : Float }
--        | #pyramid { l : Float, h : Float }

-- Shape :: #circle Float
--        | #rectangle { w : Float, h : Float }
--        | #trapezoid { t : Float, b : Float, h : Float }
--        | #square Float
--        | #triangle { b : Float, h : Float }

-- volume :: Solid -> Float
. volume = solid -> solid |>
  | #cylinder {h = h, r = r} -> h * area #circle r
  | #cube l -> l * area #square l
  | #sphere r -> 4 / 3 * pi * r * r * r
  | #cone {h = h, r = r} -> 1 / 3 * area #circle r * h
  | #pyramid {l = l, h = h} -> 1 / 3 * area #square l * h

-- area :: Shape -> Float
. area = shape -> shape |>
  | #circle r -> pi * r * r
  | #rectangle {w = w, h = h} -> w * h
  | #trapezoid {t = t, b = b, h = h} -> 1 / 2 * (t + b) * h
  | #square l -> l * l
  | #triangle {b = b, h = h} -> 1 / 2 * b * h

-- pi :: Float
. pi = 3.1415926535897932
